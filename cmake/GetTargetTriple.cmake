function(get_target_triple)
    set(options)
    set(oneValueArgs ARCH VENDOR SYS ABI TRIPLE)
    set(multiValueArgs)
    cmake_parse_arguments(ARG "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    string(TOLOWER "${CMAKE_SYSTEM_PROCESSOR}" TARGET_ARCH)
    if(TARGET_ARCH MATCHES "amd64|x86_64")
        set(TARGET_ARCH "x86_64")
    elseif(TARGET_ARCH MATCHES "aarch64|arm64")
        set(TARGET_ARCH "aarch64")
    elseif(TARGET_ARCH MATCHES "i[3-6]86")
        set(TARGET_ARCH "i386")
    else()
        set(TARGET_ARCH "unknown")
    endif()

    if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
        set(TARGET_VENDOR "pc")
        set(TARGET_SYS "linux")
        set(TARGET_ABI "gnu")
    elseif(CMAKE_SYSTEM_NAME STREQUAL "Darwin")
        set(TARGET_VENDOR "apple")
        set(TARGET_SYS "darwin${CMAKE_SYSTEM_VERSION}")
        set(TARGET_ABI "")
    elseif(CMAKE_SYSTEM_NAME STREQUAL "Windows")
        set(TARGET_VENDOR "pc")
        set(TARGET_SYS "windows")
        if(MINGW)
            set(TARGET_ABI "mingw64")
        else()
            set(TARGET_ABI "msvc")
        endif()
    else()
        set(TARGET_VENDOR "unknown")
        set(TARGET_SYS "${CMAKE_SYSTEM_NAME}")
        set(TARGET_ABI "")
    endif()

    if(ARG_ARCH)
        set(${ARG_ARCH} "${TARGET_ARCH}" PARENT_SCOPE)
    endif()

    if(ARG_VENDOR)
        set(${ARG_VENDOR} "${TARGET_VENDOR}" PARENT_SCOPE)
    endif()

    if(ARG_SYS)
        set(${ARG_SYS} "${TARGET_SYS}" PARENT_SCOPE)
    endif()

    if(ARG_ABI)
        set(${ARG_ABI} "${TARGET_ABI}" PARENT_SCOPE)
    endif()

    if(ARG_TRIPLE)
        set(TARGET_TRIPLE "${TARGET_ARCH}-${TARGET_VENDOR}-${TARGET_SYS}")
        if(TARGET_ABI)
            set(TARGET_TRIPLE "${TARGET_TRIPLE}-${TARGET_ABI}")
        endif()
        set(${ARG_TRIPLE} "${TARGET_TRIPLE}" PARENT_SCOPE)
    endif()
endfunction()
